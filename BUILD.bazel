load("@rules_java//java:java_binary.bzl", "java_binary")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_image_index", "oci_load")
load("@rules_pkg//:pkg.bzl", "pkg_tar")
load("//:build/transition.bzl", "multi_arch")

java_binary(
    name = "app",
    srcs = glob(["src/main/java/**/*.java"]),
    main_class = "com.example.HelloWorld",
    resources = glob(["src/main/resources/**"]),
    deps = [],
)

pkg_tar(
    name = "layer",
    srcs = [":app_deploy.jar"],
)

oci_image(
    name = "image",
    base = "@distroless_java",
    entrypoint = [
        "java",
        "-jar",
        "/app_deploy.jar",
    ],
    tars = [":layer"],
)

multi_arch(
    name = "images",
    image = ":image",
    platforms = [
        "//build/platforms:linux-x86_64",
        "//build/platforms:linux-aarch64",
    ],
)

oci_image_index(
    name = "index",
    images = [
        ":images",
    ],
)

oci_load(
    name = "load",
    image = ":image",
    repo_tags = ["my-repository:latest"],
)
